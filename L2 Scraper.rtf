Sub LeerAlertAgeReports()

    Dim ruta As String
    Dim archivo As String
    Dim cn As Object, rs As Object
    Dim consulta As String
    Dim destino As Range

    ruta = Environ$("USERPROFILE") & "\Downloads\"
    archivo = Dir(ruta & "AlertAgeReport_*")
    
    Set destino = ThisWorkbook.Sheets("Alert Age Report").Range("A1")

    Do While archivo <> ""

        Set cn = CreateObject("ADODB.Connection")
        Set rs = CreateObject("ADODB.Recordset")

        cn.Open "Provider=Microsoft.ACE.OLEDB.12.0;" & _
                "Data Source=" & ruta & archivo & ";" & _
                "Extended Properties=""Excel 12.0;HDR=YES;IMEX=1"";"

        consulta = "SELECT * FROM [Alert Age Report$A:F]"
        rs.Open consulta, cn

        destino.Parent.Cells(destino.Parent.Rows.Count, "A").End(xlUp).Offset(1, 0).CopyFromRecordset rs

        rs.Close
        cn.Close

        Set rs = Nothing
        Set cn = Nothing

        archivo = Dir()

    Loop

    MsgBox "Proceso terminado."

End Sub
